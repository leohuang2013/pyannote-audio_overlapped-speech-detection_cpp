if(GPU)
  if (NOT ${CMAKE_SYSTEM_NAME} STREQUAL "Linux")
    message(FATAL_ERROR "GPU is supported only Linux, you can use CPU version")
  endif()
endif()

if(${CMAKE_SYSTEM_NAME} STREQUAL "Windows")
    set(TORCH_URL "https://download.pytorch.org/libtorch/cpu/libtorch-win-shared-with-deps-2.0.1%2Bcpu.zip")
elseif(${CMAKE_SYSTEM_NAME} STREQUAL "Linux")
  if(CXX11_ABI)
    if(NOT GPU)
      set(TORCH_URL "https://download.pytorch.org/libtorch/cpu/libtorch-cxx11-abi-shared-with-deps-2.0.1%2Bcpu.zip")
    else()
      set(TORCH_URL "https://download.pytorch.org/libtorch/cu117/libtorch-cxx11-abi-shared-with-deps-2.0.1%2Bcu117.zip")
    endif()
  else()
    if(NOT GPU)
      set(TORCH_URL "https://download.pytorch.org/libtorch/cpu/libtorch-shared-with-deps-2.0.1%2Bcpu.zip")
    else()
      set(TORCH_URL "https://download.pytorch.org/libtorch/cu117/libtorch-shared-with-deps-2.0.1%2Bcu117.zip")
    endif()
  endif()
elseif(${CMAKE_SYSTEM_NAME} STREQUAL "Darwin")
    set(TORCH_URL "https://download.pytorch.org/libtorch/cpu/libtorch-macos-2.0.1.zip")
else()
  message(FATAL_ERROR "Unsupported CMake System Name '${CMAKE_SYSTEM_NAME}' (expected 'Windows', 'Linux' or 'Darwin')")
endif()

FetchContent_Declare(torch
    URL ${TORCH_URL}
)
FetchContent_MakeAvailable(torch)
include_directories(${torch_SOURCE_DIR}/include)
link_directories(${torch_SOURCE_DIR}/lib)

list(APPEND CMAKE_PREFIX_PATH "${torch_SOURCE_DIR}")

find_package(Torch REQUIRED)
set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${TORCH_CXX_FLAGS}")

if(MSVC)
  file(GLOB TORCH_DLLS "${torch_SOURCE_DIR}/lib/*.dll")

  if(CMAKE_BUILD_TYPE)
    file(COPY ${TORCH_DLLS} DESTINATION ${CMAKE_BINARY_DIR}/${CMAKE_BUILD_TYPE})
  else()
    file(COPY ${TORCH_DLLS} DESTINATION ${CMAKE_BINARY_DIR}/${CMAKE_BUILD_TYPE_INIT})
  endif()
endif()
